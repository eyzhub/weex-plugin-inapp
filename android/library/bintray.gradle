apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

version = '0.3.1'
install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                groupId 'de.eyzmedia.plugin'
                artifactId 'weexinapp'
                name 'weexinapp'
                description 'Module to perform in-app purchases in Weex platform'
                url 'https://github.com/eyzhub/weex-plugin-inapp'

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'eyzmedia'
                        name 'EYZMedia developer'
                        email 'developer@eyzmedia.de'
                    }
                }
            }
        }
    }
}
// Avoid Kotlin docs error
tasks.withType(Javadoc) {
    enabled = false
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

def bintrayUser = ''
def bintrayApiKey = ''
// Bintray
if (project.rootProject.file('local.properties').exists()) {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    bintrayUser = properties.getProperty("bintray.user")
    bintrayApiKey = properties.getProperty("bintray.apikey")
} else {
    bintrayUser = System.getenv("bintrayUser")
    bintrayApiKey = System.getenv("bintrayApiKey")
}
// Bintray
bintray {
    user = bintrayUser
    key = bintrayApiKey
    configurations = ['archives']
    pkg {
        repo = 'weexinapp'
        name = 'de.eyzmedia.plugin'
        configurations = ['archives']
        desc = 'Android module to perform in-app purchases in Weex platform'
        websiteUrl = 'https://github.com/eyzhub/weex-plugin-inapp'
        vcsUrl = 'https://github.com/eyzhub/weex-plugin-inapp.git'
        licenses = ["Apache-2.0"]
        publish = true
        publicDownloadNumbers = true
    }
}
